#
#    __                                
#   (  )                               
# \ _\/_,'    HONEYGUIDE ZSH CONFIG    
#--("))))))= -.._.-'-.._.-'-.._.-'-.._.
#     \\   
#
#
#==============================================================================
#
#             DEPENDENCIES FOR THIS .ZSHRC
#
#==============================================================================
# Core Dependencies:
# - zsh (obviously)
# - vim (used as default editor and for various functions)
# - systemd (for system-related functions)
# - dnf (package management functions)
# - rpm (package querying)
#
# External Tools:
# - fdfind (used in various search functions)
# - diff-so-fancy (for enhanced diff output)
# - viddy (for watching command output)
# - ncal (calendar display)
# - btop (system monitoring)
# - lazydocker (Docker management)
# - iotop (I/O monitoring)
# - ranger (file manager)
# - feh (image viewer)
# - batcat (enhanced cat command)
# - xclip (clipboard management)
# - img2pdf (image to PDF conversion)
# - ffmpeg (media file manipulation)
# - nasm (for assembly-related functions)
# - gcc (for C compilation)
# - python3 and pip (for Python-related functions)
# - mplayer (media player)
#
# Optional. This config contains aliases for the following utilities:
# - firefox, palemoon, google-chrome-stable, chromium-browser (web browsers)
# - xfce4-taskmanager (system monitor)
# - foot (terminal emulator)
# - virt-manager (virtualization management)
#==============================================================================

#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => COMMON SETTINGS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
autoload -U colors && colors
#PS1='%B%F{red}❯ %f%b' # for root
PS1='%B%F{green}❯ %f%b'
autoload -U promptinit
promptinit

setopt AUTO_CD
setopt auto_cd
setopt extended_glob
setopt +o nomatch
unsetopt CASE_GLOB

autoload -Uz compinit
compinit
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' 

# custom Right prompt
#setopt PROMPT_SUBST PROMPT_CR PROMPT_SP
#typeset -a no_rprompt_commands=(ls pwd e s d t nn)
#zsh_last_command=""
#preexec() { zsh_last_command="${1%% *}" }
#precmd() {
#    (( ${(@)~no_rprompt_commands[(Ie)$zsh_last_command]} )) && \
#    RPROMPT='' || RPROMPT='%F{#04548a}%1~%f'
#}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => CUSTOM HISTORY
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
HISTFILE=~/.histfile                 # Location of the history file
HISTSIZE=10000                       # Maximum events in internal history
SAVEHIST=10000                       # Maximum events in history file
setopt HIST_IGNORE_ALL_DUPS          # Remove older duplicate commands
setopt INC_APPEND_HISTORY            # Add commands to history immediately
setopt EXTENDED_HISTORY              # Save timestamp and duration
setopt HIST_EXPIRE_DUPS_FIRST        # Remove duplicates first when trimming
setopt HIST_FIND_NO_DUPS             # Don't display duplicates when searching
setopt HIST_IGNORE_SPACE             # Ignore commands starting with space
setopt SHARE_HISTORY                 # Share history between sessions
HISTORY_IGNORE='(vh|s|e|vz|n|d)'     # Ignore specific short commands

# Create and open custom history file
function vh {
    # Create a temporary file for initial processing
    temp_file=$(mktemp /tmp/histfile.XXXXXX)
    # Create a temporary file for viewing
    temp_view_file=$(mktemp /tmp/histfile_view.XXXXXX)
    
    # Process the history file using awk
    awk -F':' '
    # If line already starts with date-time format, print as is and move to next line
    /^[0-9]{2}-[0-9]{2}-[0-9]{2} [0-9]{2}:[0-9]{2}/ { print; next }
    {
        # Extract command from third field
        cmd=$3
        # Remove leading "0;" if present
        sub(/^0;/, "", cmd)
        # Format timestamp to human-readable date
        date=strftime("%y-%m-%d %H:%M", $2)
        # Print formatted date and command
        print date "| " cmd
    }' ~/.histfile > "$temp_file" && mv "$temp_file" "$temp_view_file"
    
    # Open processed history in vim for viewing
    vim "$temp_view_file"
    
    # Clean up temporary file after viewing
    rm "$temp_view_file"
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => KEY BINDINGS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
bindkey '\C-a' backward-char          # Ctrl+A to move cursor one character backward
bindkey "\C-o" accept-line            # Ctrl+F to accept the current line (like Enter)
bindkey '\C-h' up-line-or-history     # Ctrl+H to move up in history
bindkey '\C-g' down-line-or-history   # Ctrl+G to move down in history
bindkey '^[[27;5;44~' backward-word   # Ctrl+, jump word left
bindkey '^[[27;5;46~' forward-word    # Ctrl+. jump word right

# Function to delete current word or previous word
delete-word-back-or-current() {
   [[ $CURSOR -eq 0 || "${LBUFFER[-1]}" =~ [[:space:]] ]] &&
   zle backward-kill-word || { zle backward-word; zle kill-word }
}
zle -N delete-word-back-or-current
bindkey '^\' delete-word-back-or-current  # Ctrl+\ to delete current or previous word



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => ADDITIONAL COMMON SETTINGS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
alias -s mp4='mplayer -af scaletempo -really-quiet -vo xv -pausing 2'
alias -s mkv='mplayer -af scaletempo -really-quiet -vo xv -pausing 2'
alias -s mp3='mplayer -af scaletempo -really-quiet -vo xv -pausing 2'
alias -s avi='mplayer -af scaletempo -really-quiet -vo xv -pausing 2'
alias -s grec='mplayer -af scaletempo -really-quiet -vo xv -pausing 2'
alias -s mov='mplayer -af scaletempo -really-quiet -vo xv -pausing 2'
alias -s pdf='evince'
alias -s djvu='evince'
alias -s txt='vim'
alias -s odp='libreoffice'
alias -s pptx='libreoffice'

export EDITOR=vim
export VISUAL=vim
stty -ixon # disable Ctrl-S/Q flow control, freeing them for other uses



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => BASIC SYSTEM COMMANDS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# ls and facad
function enhanced_ls() {
    if [[ $# -eq 0 ]]; then
        facad
    else
        /bin/ls -A -F --group-directories-first --sort=extension --color=always "$@"
    fi
}
alias ls='enhanced_ls'

alias l='\ls | nl'
alias m='mkdir -p'
#alias cal='ncal'
alias j='\cd ../; facad;'
alias jj='\cd -; ls;'

function f {
  if [[ $1 != '' && $1 != '/' && $1 != */ ]]; then
      cd $1 && facad
  elif [[ $1 == '/' ]]; then
    \cd / && facad
  elif [[ $1 == */ ]]; then
    \cd $1 && facad
  elif [[ $1 == '-' ]]; then
    \cd -
  else
    ls
  fi
}

alias s1='systemctl poweroff'
alias s2='systemctl suspend'
alias s3='sudo reboot'
function sus {
    #echo "systemctl suspend after $1 minute" && sudo sleep $1m && sudo systemctl suspend
    echo 'disabled' | sudo tee /sys/bus/usb/devices/usb1/1-5/power/wakeup;
    sudo systemctl suspend
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => BASIC ALIASES
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
alias to='\cd /mnt/hddtb; ls'
alias to1='\cd ~/Desktop/Labour && ls'
alias to2='\cd ~/Desktop/Talks && ls'

alias vv='vim ~/.vimrc'
alias vz='vim ~/.zshrc'
alias vsw='vim ~/.config/sway/config'
alias vf='vim ~/.config/foot/foot.ini'
alias vg='vim ~/.gitconfig'
alias ii='swaymsg reload'
alias zz='source ~/.zshrc'
alias cp='cp -r'

alias x='firefox &; disown;exit'
alias pale='palemoon &; disown;exit'
alias g='google-chrome-stable &; disown;exit'
alias g2='chromium-browser &; disown;exit'
alias slimjet='flashpeak-slimjet &; disown;exit'

alias lsd='ls -d */'
alias lsff='echo "\nFiles:"; \ls -p | grep -v /|column; echo "\nDirectories-> "; tree -d -L 2|column; echo "\n";'

alias size='du -sh'
alias gsm='xfce4-taskmanager &; disown;exit'
alias io='sudo iotop --processes  --only'

function fp {
    local file_path="$1" 
    if [[ -e "$file_path" ]]
    then
        local full_path=$(readlink -f "$file_path") 
        echo " "
        echo "$full_path"
        echo " "
        echo "$full_path" | wl-copy --primary
        echo "Path copied to Primary Buffer"
    else
        echo "File are not exist"
    fi
}

alias ht='htop --sort-key=PERCENT_CPU'
alias rm='sudo rm -R' # be careful with the this command!
alias nc='ncal -M -3'
alias y='yes'
alias fss='sudo lsblk --output MODEL,TYPE,NAME,SIZE,MOUNTPOINT,FSTYPE'
alias rec='arecord -vv -fdat'
alias ff='ffmpeg -hide_banner -i'
alias tl='tldr'
alias i='cd `\cat ~/Desktop/.for_cd`; ls'
alias cw='viddy -n 1 -d "echo 'Temp'; sensors 2> /dev/null | grep Core; echo ''; echo 'Freq'; cat /proc/cpuinfo | grep \"^[c]pu MHz\""'
alias cw2='watch -n 1 -d "echo 'Temp'; sensors 2> /dev/null | grep Core; echo ''; echo 'Freq'; cat /proc/cpuinfo | grep \"^[c]pu MHz\""'
alias vm='virt-manager &; disown;exit'
alias r='ranger ./'
alias homeserv="curl ipinfo.io/ip; echo ''; hostname -I | cut -d' ' -f1; echo ''; python3 -m http.server 8095"



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => LARGE FUNCTIONS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# Install package
function rpi {
    if [ -z "$1" ]; then
        echo "Please use rpi with a package name as an argument."
    else
        sudo dnf install -y $1 && echo '' && rpm -ql $1
    fi
}

# Remove package | Be careful with this function!
function rpr() {
    sudo dnf remove $1
}

# List packages that are ready to upgrade
function rpupdl {
   sudo dnf check-update
   sudo dnf list --upgrades | nl
}

# Full upgrade
function rpflupgr() {
    sudo dnf upgrade --refresh -y
    sudo dnf autoremove -y
    sudo dnf clean all
}

# List content of package
function rpl {
   #rpm -ql $1
   rpm -ql $1 | grep -vE '^/usr/share/|^/usr/lib/\.build-id/'
}

# Search packages with specific substring
rps() {
    pkg=$1 
    dnf search "$pkg" | sed -E 's/^(.*)\.(x86_64|noarch|i686) : /\1 # /' | awk '{printf "%-20s # %s\n", $1, substr($0, index($0, "#") + 2)}'
    echo -e "\033[1mFound packages in Rep: $(dnf search "$pkg" | wc -l)\033[0m"
}


# Search packages with specific substring locally
function rpsl {
    dnf list installed | grep "$1" | cut -d'.' -f1 | nl
}

# Search packages with exact name
rpst () {
    pkg=$1
    dnf list --showduplicates "$pkg" | sed -E 's/^(.*)\.(x86_64|noarch|i686) .*/\1/' | awk '{printf "%-20s # %s\n", $1, substr($0, index($0, "#") + 2)}'
}

# List of snap packages
rpflat () {
    snap list --all | awk 'NR>1 {print $1, $2}' | column -t | nl
}

# Package statistics
function rpstat {
    local installed_count=$(dnf list installed | wc -l)
    local total_count=$(dnf list | wc -l)
    local lib_count=$(dnf list installed | grep -c 'lib')
    local python_modules=$(pip3 list --format=freeze | wc -l)
    local running_services=$(systemctl list-units --type=service --state=running --no-legend | wc -l)
    local user_installed=$(dnf history userinstalled | wc -l)
    local flatpak_count=$(flatpak list --app | wc -l)

    printf "%-20s %d (Total in repos: %d)\n" "Packages installed:" $installed_count $total_count
    printf "%-20s %d\n" "Installed by user:" $user_installed
    printf "%-20s %d\n" "Services running:" $running_services
    printf "%-20s %d\n" "Libs installed:" $lib_count
    printf "%-20s %d\n" "Python modules:" $python_modules
    printf "%-20s %d\n" "Flatpak:" $flatpak_count
}

# Find the package that provides a given command or file
function rpw {
    echo Pack: $(dnf provides $(which $1 2>/dev/null || echo $1) 2>/dev/null | grep -m1 Fedora | awk '{print $1}' | sed 's/\..*$//' || echo 'None')
}

# Show package dependencies 
function rpdn() {
    local pkg=$1
    local pkg_info=$(dnf info $pkg 2>/dev/null)
    local description=$(echo "$pkg_info" | awk -F': ' '/^Summary/ {print $2}')
    printf "\033[1m%-19s # %s\033[0m\n" "$pkg" "$description"

    local deps=$(dnf repoquery --requires --resolve "$pkg" 2>/dev/null | sort -u | grep -v "^$pkg-")
    if [[ -z "$deps" ]]; then
        echo "No package dependencies found."
        echo "Showing library dependencies:"
        deps=$(ldd $(which $pkg 2>/dev/null) 2>/dev/null | awk '{print $1}' | sort -u)
    fi

    local all_deps_info=$(dnf info $(echo "$deps" | sed 's/ [<>=].*//g') 2>/dev/null)

    echo "$deps" | while read -r dep; do
        local clean_dep=${dep%% [<>=]*}
        local desc=$(echo "$all_deps_info" | awk -v pkg="$clean_dep" '$0 ~ "^Name *: " pkg {f=1; next} f&&/^Summary *: /{print substr($0, index($0, ":") + 2); exit}')
        printf "  %-17s %s\n" "$clean_dep" "${desc:+# $desc}"
    done

    local total=$(echo "$deps" | wc -l)
    echo "\033[1mTotal Number of dependencies: $total\033[0m"
}

# List installed packages dependent on another
function rpdr {
    pkg=$1
    description=$(dnf info $1 2>/dev/null | grep 'Summary' | sed 's/Summary *: //')
    printf "\033[1m%-24s # %s\033[0m\n" "$pkg" "$description"
    dnf repoquery --installed --whatrequires "$pkg" | \
    while read -r dep; do
        desc=$(dnf info "$dep" 2>/dev/null | grep 'Summary' | sed 's/Summary *: //')
        printf "%-24s # %s\n" "$dep" "$desc"
    done
    dependency_count=$(dnf repoquery --installed --whatrequires "$pkg" | wc -l)
    echo "\033[1mDependency for: $dependency_count packages\033[0m"
}

# List repo packages depending on another
function rpdrall {
    pkg=$1
    description=$(dnf info $1 2>/dev/null | grep 'Summary' | sed 's/Summary *: //')
    printf "\033[1m%-19s # %s\033[0m\n" "$pkg" "$description"
    dnf repoquery --whatrequires "$pkg" | \
    while read -r dep; do
        desc=$(dnf info "$dep" 2>/dev/null | grep 'Summary' | sed 's/Summary *: //')
        printf "  %-17s # %s\n" "$dep" "$desc"
    done
    echo "\033[1mDependency for: $(dnf repoquery --whatrequires "$pkg" | wc -l)\033[0m"
}

# Full info about package
function rpinf() {
    if rpm -qi $1 &>/dev/null; then
        rpm -qi $1
    else
        echo "Package $1 is not installed locally. Searching for package information...\n"
        dnf info $1
    fi
}

# Get info about command or package
function wh() {
    local cmd_type cmd base result size desc contents alias_def
    cmd_type=$(whence -w "$1" | cut -d' ' -f2)
    case "$cmd_type" in
        alias)
            echo "Type: Alias"
            alias_def=$(alias "$1" | cut -d'=' -f2- | tr -d "'" 2>/dev/null)
            if [[ -n "$alias_def" ]]; then
                echo "${alias_def#*=}"
            else
                alias_def=$(which "$1" 2>/dev/null)
                if [[ -n "$alias_def" ]]; then
                    echo "Comm: ${alias_def#*=}"
                fi
            fi
            ;;
        builtin)
            echo "Type: Built-in"
            echo "Comm: $(alias | /usr/bin/which --tty-only --read-alias --show-tilde --show-dot $1)" || echo "No manual entry for $1"
            ;;
        function)
            echo "Type: Function"
            whence -f "$1"
            ;;
        command)
            cmd=$(whence -p "$1")
            echo "Link: $cmd"
            base=$(readlink -f "$cmd")
            echo "Base: $base"
            if [[ -x "$base" ]]; then
                result=$(file -b "$cmd" | cut -d',' -f1)
                echo "Type: $result"
            else
                echo "Type: $(file -b "$cmd" | cut -d',' -f1)"
            fi
            echo "Pack: $(rpm -qf "$base" 2>/dev/null)"
            size=$(du -sh "$base" 2>/dev/null | cut -f1)
            echo "Size: ${size:-N/A}"
            ;;
        *)
            if [[ "$1" == "alias" ]]; then
                cmd="/usr/bin/alias"
                echo "Link: $cmd"
                echo "Base: $cmd"
                echo "Type: $(file -b "$cmd" | cut -d',' -f1)"
                echo "Pack: $(rpm -qf "$cmd" 2>/dev/null)"
                size=$(du -sh "$cmd" 2>/dev/null | cut -f1)
                echo "Size: ${size:-N/A}"
            elif rpm -q "$1" &>/dev/null; then
                if rpm -ql "$1" | grep -q '\.so'; then
                    echo "Type: Library package"
                else
                    echo "Type: Program package"
                fi
                desc=$(rpm -qi "$1" | awk -F': ' '/^Summary/ {print $2}')
                echo "Desc: $desc"
                contents=$(rpm -ql "$1" | grep '\.so' | head -n 5)
                if [[ -n "$contents" ]]; then
                    echo "Cont: $(echo "$contents" | sed '2~1s/^/      /')"
                fi
            else
                echo "This entity doesn't exist"
            fi
            ;;
    esac
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => AUTOCOMPLETION FUNCTIONS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
_wh() {
    local -a completions
    completions+=(${(k)aliases})
    completions+=(${(k)builtins})
    completions+=(${(k)functions})
    completions+=(${(k)commands})
    compadd -a completions
}
compdef _wh wh

_rpdn() {
    local -a completions

    completions+=(${(k)commands})
    compadd -a completions
}
compdef _rpdn rpdn

_rpdr() {
    local -a completions

    completions+=(${(k)commands})
    compadd -a completions
}
compdef _rpdr rpdr

_rpl() {
    local -a completions

    completions+=(${(k)commands})
    compadd -a completions
}
compdef _rpl rpl

_rpr() {
    local -a completions

    completions+=(${(k)commands})
    compadd -a completions
}
compdef _rpr rpr

_cht() {
    local -a completions

    completions+=(${(k)commands})
    compadd -a completions
}
compdef _cht cht

_rpinfo() {
    local -a completions

    completions+=(${(k)commands})
    compadd -a completions
}
compdef _rpinfo rpinfo



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => SMALL FUNCTIONS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
alias un='unpak' # https://github.com/yellow-footed-honeyguide/unpak

function mem {
   viddy "echo  'Private  +  Shared    =  RAM used    Program'; sudo ps_mem | tac | head -n -1"
}

alias tree='\tree --dirsfirst -C'

function fbig {
    find ./ -xdev -type f -size +$@G | sed -e 's/"/"\\""/g' -e 's/.*/"&"/' |
    xargs du -sh | sort -rn | sed -e 's/.*\.$/Files not found/g'
}

function flate {
  ls -Art | tail -n $@ |
  sed -e 's/"/"\\""/g' -e 's/.*/"&"/' |
  xargs stat -c "%y %n" | sort -n
}

# Description: Removes all files in the current directory except the specified one
# Usage: rmbut <file_to_keep>
# Warning: Use with caution. This function deletes files!
# Note: Creates a temporary directory ../.rmbut for the operation
function rmbut {
     mkdir ../.rmbut &&
     mv $1 ../.rmbut &&
     rm * &&
     mv ../.rmbut/$1 ./ &&
     rm ../.rmbut
}

function gre {
    grep -nir $1 * | sed '/Binary/d' | nl 
}

function loc {
    fd "$1" . 2>&1 | grep -v "Permission denied" | nl
}

function mir {
  img2pdf * --output slideshow.pdf && evince slideshow.pdf && rm ./slideshow.pdf
}

function ca {
  \cat *.mp3  | ffmpeg  -i pipe: -c:a copy -c:v copy $1 && mv $1 .. && cd .. && ls
}

function cht {
    /usr/local/bin/cht.sh $1 | sed 's/\x1B\[[0-9;]\{1,\}[A-Za-z]//g' > ~/.cheat/temp_output_cht_sh && vim ~/.cheat/temp_output_cht_sh && clear && pwd
}

function gc {
   gcc $1 && ./a.out
}

function na {
    filename=$1;
    filename="${filename%.s}";
    nasm -f elf64 -o $filename.o $filename.s && ld -o $filename $filename.o && ./$filename && rm ./*.o
}

function di() {
    diff -Nurs "$1" "$2" | diff-so-fancy
}

# Function: trim
# Description: Trims a video file using ffmpeg
# Usage: trim <input_file> <trim_start> <trim_end>
# Arguments:
#   <input_file>: Path to the input video file
#   <trim_start>: Cut first n seconds from video
#   <trim_end>: Cut last n seconds from video
# Example: trim input.mp4 3 5
# As Resulf: inpur_trim.mp4 without first 3 sec and last 5 sec
# Note: Requires ffmpeg and ffprobe to be installed
trim() {
    if [[ $# -ne 3 ]]; then
        echo "Usage: video_trim <input_file> <trim_start> <trim_end>"
        return 1
    fi

    local input_file="$1"
    local trim_start="$2"
    local trim_end="$3"
    local output_file="${input_file%.*}_trimmed.${input_file##*.}"

    if [[ ! -f "$input_file" ]]; then
        echo "Error: Input file does not exist."
        return 1
    fi

    local duration=$(ffprobe -v error -show_entries format=duration -of default=noprint_wrappers=1:nokey=1 "$input_file")
    local end_time=$(echo "$duration - $trim_end" | bc)

    ffmpeg -i "$input_file" -ss "$trim_start" -to "$end_time" -c copy -threads 0 "$output_file"


    if [[ $? -eq 0 ]]; then
        echo "Video trimmed successfully. Output file: $output_file"
    else
        echo "Error occurred while trimming the video."
        return 1
    fi
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => SET WINDOW TITLE FOR TERMINAL EMULATORS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# Usage: add to .zshrc and ensure zsh hooks are loaded
set_win_title() {
    # Check if terminal supports setting window title
    if [[ -n "$TERM_PROGRAM" ]] || [[ "$TERM" =~ ^(xterm|rxvt|foot|alacritty|gnome|konsole|iTerm|mintty|terminator|termite|kitty) ]]; then
        if [[ "$1" =~ ^vim.* ]]; then
            print -Pn "\e]0;vim: ${1#vim }\a"  # Show edited file name for vim
        elif [[ -n "$1" ]]; then
            print -Pn "\e]0;$1\a"  # Show full command with arguments
        else
            print -Pn "\e]0;%~\a"  # Show current directory by default
        fi
    fi
}

# Set window title to current directory before each prompt
set_win_title_precmd() { print -Pn "\e]0;%~\a" }

# Load zsh hooks
autoload -Uz add-zsh-hook
add-zsh-hook preexec set_win_title
add-zsh-hook precmd set_win_title_precmd



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => Systemd functions
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
rpservs () {
  # STEP 1. List running services using systemctl and format the output
  systemctl list-units --type=service --state=running --no-pager | grep service | sed 's/loaded active running//g' | \
  awk '{print "\n\033[1m" NR " | " $1;

  # STEP 2.  Extract and display the Description of the service
  system("systemctl show -p Description " $1 " | \
  sed \"s/Description=//\" | \
  awk \"{print \\\"\033[0mDesc: \\\" \\$0}\"");

  # STEP 3. Extract and display the ExecStart (binary path) of the service
  system("systemctl cat " $1 " | \
  grep -E \"^ExecStart=\" | \
  sed \"s/ExecStart=//\" | \
  awk \"{gsub(/\\\\.\\\\//, \\\" \\\"); print \\\"Exec: \\\" \\$0}\"");

  # STEP 4. Extract and display the actual binary file path (BinA) using readlink -f
  system("systemctl cat " $1 " | \
  grep -E \"^ExecStart=\" | \
  sed \"s/ExecStart=//\" | \
  awk \"{gsub(/\\\\.\\\\//, \\\" \\\"); system(\\\"readlink 2>/dev/null -f \\\" \\$1)}\" | \
  awk \"{print \\\"BinA: \\\" \\$0}\"");

  # STEP 5. Extract the binary file size (Size) of the service
  system("systemctl cat " $1 " | \
  grep -E \"^ExecStart=\" | \
  sed \"s/ExecStart=//\" | \
  awk \"{gsub(/\\\\.\\\\//, \\\" \\\"); system(\\\"du 2>/dev/null -sh \\\" \\$1)}\" | \
  awk \"{print \\\"Size: \\\" \\$1}\"");

  # STEP 6. Extract and display the startup time of the service using systemd-analyze blame
  # system("systemd-analyze blame | grep " $1 " | \
  # awk \"{print \\\"Time: \\\" \\$1}\"");

  # STEP 7. Extract and display the config file path (FragmentPath) of the service
  system("systemctl show " $1 " -p FragmentPath | \
  sed \"s/FragmentPath=//\" | \
  awk \"{print \\\"Conf: \\\" \\$0}\"");}'
}

rpblame() {
  systemd-analyze blame  --no-pager | nl
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => Python things
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
alias py='python3'
alias spu='sudo python3 -m pip install --upgrade'
alias spr='sudo python3 -m pip uninstall'
alias spsh='sudo python3 -m pip show'
alias spf='sudo python3 -m pip freeze | nl'
alias sps='sudo python3 -m pip search'


function spi { 
  sudo python3 -m pip install $1 --break-system-packages 
}

function spv {
  sudo python3 -m pip install  $1==
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => Mplayer things
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
alias mp="mplayer -af scaletempo -really-quiet -vo xv -pausing 2"
alias mptime="mplayer -af scaletempo -really-quiet -vo xv -pausing 2 -ss"
alias mpv="mplayer -af scaletempo -really-quiet -vo xv -pausing 2 -ss 00:00:44"
alias mpvg="mplayer -af scaletempo -really-quiet -vo xv -pausing 2 -ss 01:42:45"
alias mpvh="mplayer -af scaletempo -really-quiet -vo xv -pausing 2 -ss 00:03:45"
alias mps='mplayer -af scaletempo -vo xv -pausing 2 -shuffle * 2>/dev/null  | grep Playing'
function mpr {
 echo '' && mprr $1 && echo '' && ls
}
function mprs {
 echo '' && mprr -slave -input file=/tmp/mplayer-control $1 && echo '' && ls
}



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => VI-MODE
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
bindkey -v

# Function to update prompt based on vi mode
function zle-line-init zle-keymap-select {
   case ${KEYMAP} in
       vicmd)
           PROMPT='%B%F{black}✎ %f%b'
           ;;
       main|viins)
           PROMPT='%B%F{green}❯ %f%b'
           ;;
   esac
   zle reset-prompt
}
zle -N zle-line-init
zle -N zle-keymap-select

# Always start in insert mode and set the prompt accordingly
zle-line-init() {
   zle -K viins
   PROMPT='%B%F{green}❯ %f%b'
   zle reset-prompt
}
zle -N zle-line-init

# Switch between normal and edit mode with Ctrl+F only
bindkey -M viins '^F' vi-cmd-mode
bindkey -M vicmd '^F' vi-insert

# Move to end of line and enter edit mode with 'n'
bindkey -M vicmd 'n' vi-add-eol

# Normal mode keybindings
bindkey -M vicmd 'd' backward-word
bindkey -M vicmd 'k' forward-word
bindkey -M vicmd 'j' accept-line
bindkey -M vicmd ',' undo
bindkey -M vicmd '.' redo
bindkey -M vicmd 'h' up-line-or-history
bindkey -M vicmd 'g' down-line-or-history
bindkey -M vicmd 'c' kill-whole-line
bindkey -M vicmd 'D' beginning-of-line
bindkey -M vicmd 'K' end-of-line
bindkey -M vicmd 'v' kill-word
bindkey -M vicmd 'a' backward-char
bindkey -M vicmd 'w' forward-char
bindkey -M vicmd 'f' delete-char
bindkey -M vicmd 'E' kill-line
bindkey -M vicmd 'o' clear-screen

# Function to delete word (regardless of cursor position)
kill-word() {
   zle set-mark-command
   zle forward-word
   zle backward-kill-word
}
zle -N kill-word

# Paste from system primary buffer in normal mode
#bindkey -M vicmd 'j' vi-put-after

# Disable all keys in insert mode except Ctrl+F
bindkey -M viins '^F' vi-cmd-mode

# Restrict input in normal mode
bindkey -M vicmd -r 'a-zA-Z0-9'

# Set a short timeout for key sequences
export KEYTIMEOUT=1



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => PLUGINS
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# Load zsh-syntax-highlighting; should be last.
source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
ZSH_HIGHLIGHT_STYLES[command]='fg=green,bold'        # Commands in green and bold
ZSH_HIGHLIGHT_STYLES[alias]='fg=green,bold'          # Aliases in green and bold
ZSH_HIGHLIGHT_STYLES[builtin]='fg=green,bold'        # Built-in commands in green and bold
ZSH_HIGHLIGHT_STYLES[function]='fg=green,bold'       # Functions in green and bold
ZSH_HIGHLIGHT_STYLES[command-substitution]='fg=green,bold'  # Command substitutions in green and bold
ZSH_HIGHLIGHT_STYLES[precommand]='fg=green,bold'     # Precommands (like 'sudo') in green and bold
ZSH_HIGHLIGHT_STYLES[single-hyphen-option]='fg=blue' # Single-hyphen options in blue
ZSH_HIGHLIGHT_STYLES[double-hyphen-option]='fg=blue' # Double-hyphen options in blue
ZSH_HIGHLIGHT_STYLES[arg0]='fg=gray'                 # First argument in gray
ZSH_HIGHLIGHT_STYLES[arg1]='fg=gray'                 # Second argument in gray
ZSH_HIGHLIGHT_STYLES[path]='fg=black'                # Paths in black (default terminal color)

# Reset colors for various elements
ZSH_HIGHLIGHT_STYLES[default]='none'                 # Default style (for unmatched elements)
ZSH_HIGHLIGHT_STYLES[unknown-token]='none'           # Unknown tokens (including incorrect commands)
ZSH_HIGHLIGHT_STYLES[reserved-word]='none'           # Reserved words
ZSH_HIGHLIGHT_STYLES[globbing]='none'                # Globbing expressions
ZSH_HIGHLIGHT_STYLES[history-expansion]='none'       # History expansions
ZSH_HIGHLIGHT_STYLES[comment]='none'                 # Comments
ZSH_HIGHLIGHT_STYLES[dollar-double-quoted-argument]='none'  # Dollar-quoted arguments
ZSH_HIGHLIGHT_STYLES[back-double-quoted-argument]='none'    # Backtick-quoted arguments
ZSH_HIGHLIGHT_STYLES[assign]='none'                  # Variable assignments

# Disable underlining for paths
ZSH_HIGHLIGHT_STYLES[path_prefix]='none'
ZSH_HIGHLIGHT_STYLES[path_approx]='none'

# Reset colors for quoted strings
ZSH_HIGHLIGHT_STYLES[double-quoted-argument]='none'  # Strings in double quotes
ZSH_HIGHLIGHT_STYLES[single-quoted-argument]='none'  # Strings in single quotes
ZSH_HIGHLIGHT_STYLES[dollar-quoted-argument]='none'  # Strings in $'...' quotes



#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
# => Misc
#""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""""
: <<COMMENT
This is examle of multi-lines comments
COMMENT
